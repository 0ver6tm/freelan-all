import os
import sys
import datetime


major = 2
minor = 0

Import('env dirs name')

libraries = [
    'freelan',
    'asiotap',
    'fscp',
    'cryptoplus',
    'iconvplus',
    'kfather',
    'boost_system',
    'boost_thread',
    'boost_filesystem',
    'boost_date_time',
    'boost_program_options',
    'curl',
    'ssl',
    'crypto',
    'ldap',
    'z',
]

if sys.platform.startswith('linux'):
    libraries.extend([
        'pthread',
    ])

env = env.Clone()
env.Append(LIBS=libraries)
env['CXXFLAGS'].append(['-DFREELAN_VERSION_MAJOR=%s' % major])
env['CXXFLAGS'].append(['-DFREELAN_VERSION_MINOR=%s' % minor])
env['CXXFLAGS'].append([r"-DFREELAN_DATE=\"%s\"" % datetime.date.today().strftime('%a %d %b %Y')])

sources = list(sorted(set(env.RGlob('src', '*.cpp')) - set(env.RGlob(os.path.join('src', 'windows'), '*.cpp'))))
app = env.Program(target=os.path.join(str(dirs['bin']), name), source=sources)

Return('app')
