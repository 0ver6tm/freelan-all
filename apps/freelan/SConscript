import os
import sys
import datetime


major = 2
minor = 0

Import('env dirs name')

libraries = [
    'freelan',
    'asiotap',
    'fscp',
    'cryptoplus',
    'executeplus',
    'iconvplus',
    'kfather',
    'boost_system',
    'boost_thread',
    'boost_filesystem',
    'boost_date_time',
    'boost_program_options',
    'boost_iostreams',
    'curl',
    'ssl',
    'crypto',
]

if sys.platform.startswith('linux'):
    libraries.extend([
        'pthread',
        'netlinkplus',
    ])
elif sys.platform.startswith('darwin'):
    libraries.extend([
        'ldap',
        'z',
    ])

env = env.Clone()
env.Append(LIBS=libraries)
env['CXXFLAGS'].append(['-DFREELAN_VERSION_MAJOR=%s' % major])
env['CXXFLAGS'].append(['-DFREELAN_VERSION_MINOR=%s' % minor])
env['CXXFLAGS'].append([r"-DFREELAN_DATE=\"%s\"" % datetime.date.today().strftime('%a %d %b %Y')])

sources = list(sorted(set(env.RGlob('src', '*.cpp')) - set(env.RGlob(os.path.join('src', 'windows'), '*.cpp'))))
app = env.Program(target=os.path.join(str(dirs['bin']), '%s%s' % (name, major)), source=sources)

Return('app')
