##
# libcryptoplus tests build file.
#

### YOU SHOULD NEVER CHANGE ANYTHING BELOW THIS LINE ###

module = 'cryptoplus'

import sys, os, subprocess

source = Glob(os.path.join(sys.path[0], 'src/*.cpp'))
cxxflags = subprocess.Popen(['cppunit-config', '--cflags'], stdout=subprocess.PIPE).communicate()[0].split()
cpppath = [os.path.join(sys.path[0], '../include', module)]
libpath = [os.path.join(sys.path[0], '../lib')]
libs = ['crypto', module + '_static'] + subprocess.Popen(['cppunit-config', '--libs'], stdout=subprocess.PIPE).communicate()[0].split()

# Import the customized environment
sys.path.append(os.path.abspath(os.path.join(sys.path[0], '../scons')))
import environment

env = environment.Environment(ENV = os.environ.copy())

# Build the test
tests = env.Program(os.path.join(sys.path[0], 'tests'), source, CPPPATH = cpppath, LIBPATH = libpath, LIBS = libs)

# Aliases
env.Alias('build', tests)
run = env.Alias('run', [tests], tests[0].abspath)
env.Alias('all', ['build', 'run'])

env.AlwaysBuild(run);

# Help documentation
Help("""
Type: 'scons build' to build the unit tests.
      'scons run' to run the tests.
      'scons all' to build then run the tests.
      'scons -c' to cleanup object and libraries files.
      \n
      If scons is called without parameters, the default target is "all".
""")

# Default
Default('all')
