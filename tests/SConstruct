##
# libcryptoplus tests build file.
#

### YOU SHOULD NEVER CHANGE ANYTHING BELOW THIS LINE ###

module = 'cryptoplus'

import sys, os, subprocess

source = Glob('src/*.cpp')
cxxflags = subprocess.Popen(['cppunit-config', '--cflags'], stdout=subprocess.PIPE).communicate()[0].split()
cpppath = [os.path.join('../include', module)]
libpath = [os.path.join('../lib')]
libs = ['crypto', module] + subprocess.Popen(['cppunit-config', '--libs'], stdout=subprocess.PIPE).communicate()[0].split()

# Import the customized environment
sys.path.append(os.path.abspath('../scons'))
import environment

env = environment.Environment(ENV = os.environ.copy())

# Build the test
tests = env.Program('tests', source, CPPPATH = cpppath, LIBPATH = libpath, LIBS = libs)

# Aliases
env.Alias('build', tests)
run = env.Alias('run', [tests], tests[0].abspath)
env.Alias('all', ['build', 'run'])

env.AlwaysBuild(run);

# Help documentation
Help("""
Type: 'scons build' to build the unit tests.
      'scons run' to run the tests.
      'scons all' to build then run the tests.
      'scons -c' to cleanup object and libraries files.
      \n
      If scons is called without parameters, the default target is "all".
""")

# Default
Default('all')
